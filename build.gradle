plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'ninja.miserable.blossom' version '1.0.1'
}

group = 'net.kaikk.mc'
version = '3.8.0-S7.1.0-dev'
description = 'Better Chunkloading.'
project.ext.authors = 'KaiNoMood, Rob5Underscores, KasperFranz'


dependencies {
    compile 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'
    compile 'com.github.KasperFranz:BCLForgeLib:master'
    compile 'org.bstats:bstats-sponge:1.7'
}

blossom {
    def location = 'src/main/java/guru/franz/mc/bcl/BetterChunkLoaderPluginInfo.java'

    replaceToken '@name@', project.name, location
    replaceToken '@version@', project.version+"-"+getGitHash(), location
    replaceToken '@description@', project.description, location
}

static String getGitHash() {
    def process = 'git rev-parse --short HEAD'.execute()
    process.waitFor()
    return process.exitValue() ? 'unknown' : process.text.trim()
}


repositories {
    jcenter()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.codemc.org/repository/maven-public/'}
}

shadowJar {
    dependencies {
        include(dependency("org.bstats:bstats-sponge:1.7"))
    }
    classifier = ''
}

artifacts {
    archives shadowJar
}